CREATE PROCEDURE [dbo].[USP_ADDUSERCHARGE]
@MESSAGEIDI		INT,			--消息类型
@SEQUENCEI		BIGINT,			--PASS9订单序列号
@USERID			VARCHAR(100),	--PASS9帐号
@CARDTYPE		CHAR(4),		--充值卡类型
@CARDNO			VARCHAR(15),	--卡号或者订单号
@TXNDATE		DATETIME,		--对账日
@METHOD			INT,			--包月/记点标志位1.包月;2.记点
@QUANTITY		INT,			--数量
@BONUS			INT,			--奖励数量
@MEMO			VARCHAR(100),	--备注信息
@MESSAGEID		INT OUTPUT,		--消息类型
@SEQUENCE		BIGINT OUTPUT,	--PASS9订单序列号
@RESULT			CHAR(4) OUTPUT
AS
BEGIN
	SET NOCOUNT ON
	SET XACT_ABORT ON
	DECLARE @HONOR INT
	DECLARE @AccountID INT
	SELECT @AccountID = 0,@HONOR = 0,@MESSAGEID = @MESSAGEIDI,@SEQUENCE = @SEQUENCEI,@RESULT='0000'
	IF @QUANTITY < 0
		SET @RESULT = '0001'

	IF @RESULT = '0000' AND 
		EXISTS(SELECT 1 FROM DBO.GAME_CHARGE_HISTORY WHERE SEQUENCE = @SEQUENCE)
		SET @RESULT = '0010'

	SELECT @AccountID = AccountID FROM SXZ_ACCOUNTDB.DBO.TBL_ACCOUNT A WHERE ACCOUNT = @USERID
	IF @RESULT = '0000' AND @@ROWCOUNT = 0
		SET @RESULT = '0003'

	IF @RESULT = '0000'
	BEGIN
		BEGIN TRAN ADDUSERCHARGE
		IF NOT EXISTS(SELECT 1 FROM T_USER_PAY WHERE ACCOUNTID = @AccountID)
			INSERT dbo.T_USER_PAY SELECT @AccountID,0,0,0

		UPDATE DBO.T_USER_PAY
			SET PAY_POINTS = PAY_POINTS + @QUANTITY, FREE_POINTS = FREE_POINTS + @BONUS
			WHERE ACCOUNTID = @AccountID
		IF @@ERROR <> 0 
			SET @RESULT = '0004'

		IF @RESULT = '0000'
		BEGIN
			INSERT INTO GAME_CHARGE_HISTORY(SEQUENCE,CREATEDATE,CHECKDATE,ACCOUNTID,
					PAY_POINTS,FREE_POINTS,HONOR,CARDTYPE,CARDNO)
				SELECT @SEQUENCEI,GETDATE(),@TXNDATE,ACCOUNTID,@QUANTITY,@BONUS,@HONOR,@CARDTYPE,@CARDNO
					FROM SXZ_ACCOUNTDB.DBO.TBL_ACCOUNT 
					WHERE ACCOUNT = @USERID
			IF @@ERROR <> 0 
				SET @RESULT = '0004'
		END

		IF @RESULT = '0000'
			COMMIT TRAN ADDUSERCHARGE
		ELSE
			ROLLBACK TRAN ADDUSERCHARGE
   END
END